// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
FistWeapons is Weapon

constants:

   include blakston.khd

resources:
  
   FistWeapons_name_rsc = "Fist Weapons"
   FistWeapons_icon_rsc = gauntlet.bgf
   FistWeapons_desc_rsc = \
      "These polished steel FistWeaponss offer protection for "
      "hands and forearms allowing you to strike with "
      "greater confidence and force."

   FistWeapons_condition_exc = " shine with the glint of perfection."
   FistWeapons_condition_exc_mended = \
      " are in excellent condition, but have been patched before."
   FistWeapons_condition_good = " are tarnished and scarred but still usable."
   FistWeapons_condition_med = " have been harshly dented in several places."
   FistWeapons_condition_poor = \
      " are in danger of falling apart.  Large plates of metal "
      "have been torn away during battle."
   FistWeapons_condition_broken = " are ruined and offer no benefit."
   FistWeapons_broken = "Your %s are a tangled mess of metal."

   standardshirt_rightarm_FistWeapons_male = bre.bgf
   standardshirt_rightarm_FistWeapons_female =brf.bgf
   standardshirt_leftarm_FistWeapons_male = ble.bgf
   standardshirt_leftarm_FistWeapons_female = blf.bgf

   barearm_rightarm_FistWeapons_male = bri.bgf
   barearm_rightarm_FistWeapons_female =brj.bgf
   barearm_leftarm_FistWeapons_male = bli.bgf
   barearm_leftarm_FistWeapons_female = blj.bgf

classvars:

   vrName = FistWeapons_name_rsc
   vrIcon = FistWeapons_icon_rsc
   vrDesc = FistWeapons_desc_rsc
   vrPoss_article = object_article_cap_these_rsc

   vrItem_broken = FistWeapons_broken

   viUse_type = ITEM_USE_GAUNTLET
   viItem_type = ITEMTYPE_WEAPON
   viUse_amount = 1

   viHits_init_min = 500
   viHits_init_max = 1000

   viValue_average = 1500
   viBulk = 20
   viWeight = 20

   viGround_group = 1
   viInventory_group = 2
   viBroken_group = 3

   viRange = 2 * FINENESS

   vbShow_condition = TRUE

   vrCondition_exc = FistWeapons_condition_exc 
   vrCondition_exc_mended = FistWeapons_condition_exc_mended 
   vrCondition_good = FistWeapons_condition_good 
   vrCondition_med = FistWeapons_condition_med 
   vrCondition_poor = FistWeapons_condition_poor 
   vrCondition_broken = FistWeapons_condition_broken

   viWeaponType = WEAPON_TYPE_BLUDGEON
   viWeaponQuality = WEAPON_QUALITY_HIGH
   viProficiency_Needed = SKID_BRAWLING

properties:

   piDamageBonus = 8
   piHitBonus = 125

   piAttack_type = ATCK_WEAP_UNARMED+ATCK_WEAP_PUNCH

messages:

   ReqUseSomething(what = $)
   {
      if IsClass(what,&Gauntlet)
      {
         if Send(poOwner,@TryUnuseItem,#what=self)
         {
            propagate;
         }
         else
         {
            return FALSE;
         }
      }

      propagate;
   }

   NewUsed(what = $)
   {
      local i, oFound, iPriority;

      oFound=$;
      foreach i in Send(what,@GetPlayerUsing)
      {
         iPriority = Send(i,@ItemChangesArms);
         if iPriority <> 0
         {
            if oFound = $ 
            {
               oFound = i;
            }
            else
            {
               if iPriority > Send(oFound,@ItemChangesArms)
               {
                  oFound = i;
               }
            }
         }
      }

      if oFound <> $
      {
         Send(oFound,@DoPlayerArt);
      }
      else
      {
         Send(what,@ResetPlayerArms);
      }

      // We're also a defense modifier
      Send(what,@AddDefenseModifier,#what=self);

      propagate;
   }

   NewUnused(what = $)
   {
      local i, oFound, iPriority;

      oFound=$;
      foreach i in Send(what,@GetPlayerUsing)
      {
         iPriority = Send(i,@ItemChangesArms);
         if iPriority <> 0
         {
            if oFound <> $ 
            {
               if iPriority > Send(oFound,@ItemChangesArms)
               {
                  oFound = i;
               }
            }
            else
            {
               oFound = i;
            }
         }
      }

      if oFound <> $
      {
         Send(oFound,@DoPlayerArt); 
      }
      else
      {
         Send(what,@ResetPlayerArms);
      }

      // We're also a defense modifier
      Send(what,@RemoveDefenseModifier,#what=self);

      propagate;
   }

   OverrideArms()
   {
      return TRUE;
   }

   GetOverrideRightArm(who=$)
   {
      local i, bFound;

      bFound = FALSE;
      foreach i in Send(who,@GetPlayerUsing)
      {
         if IsClass(i,&TankTop)
         {
            bFound = TRUE;

            break;
         }
      }

      if bFound
      {
         if Send(who,@GetGender) = GENDER_MALE
         {
            return barearm_rightarm_FistWeapons_male;
         }
      
         return barearm_rightarm_FistWeapons_female;
      }

      if Send(who,@GetGender) = GENDER_MALE
      {
         return standardshirt_rightarm_FistWeapons_male;
      }

      return standardshirt_rightarm_FistWeapons_female;
   }

   GetOverrideLeftArm(who=$)
   {
      local i, bFound;

      bFound = FALSE;
      foreach i in Send(who,@GetPlayerUsing)
      {
         if IsClass(i,&TankTop)
         {
            bFound = TRUE;

            break;
         }
      }

      if bFound
      {
         if Send(who,@GetGender) = GENDER_MALE
         {
            return barearm_leftarm_FistWeapons_male;
         }
      
         return barearm_leftarm_FistWeapons_female;
      }

      if Send(who,@GetGender) = GENDER_MALE
      {
         return standardshirt_leftarm_FistWeapons_male;
      }

      return standardshirt_leftarm_FistWeapons_female;
   }

   CanMend()
   {
      local i, oItemAtt;

      foreach i in plItem_Attributes
      {
         oItemAtt = Send(SYS,@FindItemAttByNum,
                         #num=Send(self,@GetNumFromCompound,#compound=First(i)));
         if oItemAtt = $
         {
            Debug("Illegal ItemAtt in list!");

            continue;
         }

         if NOT Send(oItemAtt,@ItemCanMend,#oItem=self)
         {
            return FALSE;
         }
      }

      return TRUE;
   }
   
   ModifyDamage(damage = $)
   {
      return damage;
   }

   ModifyDefenseDamage(who = $,what = $,damage = $)
   {
      return damage;
   }

   WeaponAttack(what=$)
   "Called by ReqWeaponAttack when the weapon is actually used in an attack."
   {
      send(poOwner,@DoFistAttack);

      propagate;
   }

   GetDefaultStrokeNumber()
   {
      return SKID_PUNCH;
   }

end
////////////////////////////////////////////////////////////////////////////////
